// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0


    PARTS:

    Mux16(a=x, b=false, sel=zx, out=resultXMaybeZero);
    Not16(in=resultXMaybeZero, out=resultXMaybeOne);
    Mux16(a=resultXMaybeZero, b=resultXMaybeOne, sel=nx, out=resultX);

    Mux16(a=y, b=false, sel=zy, out=resultYMaybeZero);
    Not16(in=resultYMaybeZero, out=resultYMaybeOne);
    Mux16(a=resultYMaybeZero, b=resultYMaybeOne, sel=ny, out=resultY);

    Add16(a=resultX, b=resultY, out=resultXPlusY);
    And16(a=resultX, b=resultY, out=resultXAndY);

    Mux16(a=resultXAndY, b=resultXPlusY, sel=f, out=result);
    Not16(in=result, out=notResult);
    Mux16(a=result, b=notResult, sel=no, out=finalResult, 
        out[0..7]=bFirst,
        out[8..15]=bSecond);

    Not16(in=finalResult, out=notFinalResult);
    Not16(in=notFinalResult, out=out);

    And16(a=finalResult, b=true, out[15]=ng);

    Or8Way(in=bFirst, out=notZrFirst);
    Or8Way(in=bSecond, out=notZrSecond);
    Or(a=notZrFirst, b=notZrSecond, out=notZr);
    Not(in=notZr, out=zr);
}